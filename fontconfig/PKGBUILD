# Contributor: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>

_realname=fontconfig
pkgbase=${_realname}
pkgname="${_realname}"
pkgver=2.15.0
pkgrel=2
pkgdesc="A library for configuring and customizing font access"
arch=('i686' 'x86_64')
url="https://wiki.freedesktop.org/www/Software/fontconfig/"
msys2_repository_url="https://gitlab.freedesktop.org/fontconfig/fontconfig"
msys2_references=(
  "cpe: cpe:/a:fontconfig_project:fontconfig"
)
license=("custom")
makedepends=('gcc'
             'libexpat-devel'
             'meson'
             'gperf')
depends=('gcc-libs'
         'expat'
         'freetype')
#install=${_realname}.install
source=("https://www.freedesktop.org/software/fontconfig/release/fontconfig-${pkgver}.tar.xz"
        #0010-fix-relocation.patch
        #0011-conf-copy-instead-of-symlink.patch
        #fontconfig.hook.in
        https://gitlab.freedesktop.org/fontconfig/fontconfig/-/commit/a76b95105ca26c600279b2daf4bd43b37a918823.patch)
sha256sums=('63a0658d0e06e0fa886106452b58ef04f21f58202ea02a94c39de0d3335d7c0e'
            #'1506f01dcc7c8b80a9e7fbe56720742bd9134c8e4c3321579fd44dbe1aac028d'
            #'73ed74a1f4624466084d219e2fbc0d5780da9f63763f1307629251e58cccf2cd'
            #'ad4ce48983045d7f39b40ca9e04d4be2eaa9177b89dcc516388d779c131a88bc'
            'c225c156c4732a492c9588c8d33705ca29af6f3195f201f4012b09ff012f1831')

prepare() {
  cd "${_realname}-${pkgver}"

  # backport because it seems relevant for us
  patch -p1 -i ${srcdir}/a76b95105ca26c600279b2daf4bd43b37a918823.patch

  #patch -p1 -i ${srcdir}/0010-fix-relocation.patch
  #patch -p1 -i ${srcdir}/0011-conf-copy-instead-of-symlink.patch
}

build() {
  mkdir -p "build-${CARCH}-static" && cd "build-${CARCH}-static"

    meson setup \
      --default-library=static \
      --prefix="${MSYSTEM_PREFIX}" \
      --wrap-mode=nodownload \
      --auto-features=enabled \
      --buildtype=plain \
      -Ddoc-man=disabled \
      -Ddoc-txt=disabled \
      -Ddoc-pdf=disabled \
      -Ddoc-html=disabled \
      -Dcache-build=disabled \
      ../"${_realname}-${pkgver}"

  meson compile

  cd ..
  mkdir -p "build-${CARCH}-shared" && cd "build-${CARCH}-shared"

    meson setup \
      --default-library=shared \
      --prefix="${MSYSTEM_PREFIX}" \
      --wrap-mode=nodownload \
      --auto-features=enabled \
      --buildtype=plain \
      -Ddoc-man=disabled \
      -Ddoc-txt=disabled \
      -Ddoc-pdf=disabled \
      -Ddoc-html=disabled \
      -Dcache-build=disabled \
      ../"${_realname}-${pkgver}"

  meson compile
}

package() {
  cd "${srcdir}/build-${CARCH}-static"
  DESTDIR="${pkgdir}" meson install

  cd "${srcdir}/build-${CARCH}-shared"
  DESTDIR="${pkgdir}" meson install

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MSYSTEM_PREFIX}/share/licenses/${_realname}/COPYING"

  sed -i "s|${MSYSTEM_PREFIX}/etc$|\${prefix}/etc|g" "${pkgdir}${MSYSTEM_PREFIX}"/lib/pkgconfig/fontconfig.pc
  sed -i "s|${MSYSTEM_PREFIX}/etc/|\${sysconfdir}/|g" "${pkgdir}${MSYSTEM_PREFIX}"/lib/pkgconfig/fontconfig.pc
  sed -i "s|${MSYSTEM_PREFIX}/var|\${prefix}/var|g" "${pkgdir}${MSYSTEM_PREFIX}"/lib/pkgconfig/fontconfig.pc

  #for hook in fontconfig; do
    #local hook_path="${srcdir}/${MINGW_PACKAGE_PREFIX}-${hook}.hook";
    #cp "${srcdir}/${hook}.hook.in" "${hook_path}"
    #sed -s "s|@MINGW_HOOK_TARGET_PREFIX@|${MSYSTEM_PREFIX:1}|g" -i "${hook_path}"
    #sed -s "s|@MSYSTEM_PREFIX@|${MSYSTEM_PREFIX}|g" -i "${hook_path}"
    #install -Dt "$pkgdir/usr/share/libalpm/hooks" -m644 "${hook_path}"
  #done
}
