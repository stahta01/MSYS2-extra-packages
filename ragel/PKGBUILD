# Contributor: Ebrahim Byagowi <ebrahim@gnu.org>

_realname=ragel
pkgbase=${_realname}
pkgname="${_realname}"
pkgver=6.10
pkgrel=1
pkgdesc="Compiles finite state machines from regular languages into executable C, C++, Objective-C, or D code"
arch=('i686' 'x86_64')
url="https://www.colm.net/open-source/ragel/"
license=('GPL')
depends=('gcc-libs')
makedepends=('gcc' 'autotools')
source=("https://www.colm.net/files/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('5f156edb65d20b856d638dd9ee2dfb43285914d9aa2b6ec779dac0270cd56c3f')

build() {
  [[ -d "build-${MSYSTEM_CARCH}" ]] && rm -rf "build-${MSYSTEM_CARCH}"
  mkdir -p "build-${MSYSTEM_CARCH}"
  cd "build-${MSYSTEM_CARCH}"

  ../${_realname}-${pkgver}/configure \
    --prefix=${MSYSTEM_PREFIX} \
    --build=${MSYSTEM_CHOST} \
    --host=${MSYSTEM_CHOST}

  make
}

package() {
  cd "${srcdir}/build-${MSYSTEM_CARCH}"

  make DESTDIR="${pkgdir}" install
  install -m0644 -D ${srcdir}/${_realname}-${pkgver}/ragel.vim "${pkgdir}${MSYSTEM_PREFIX}"/share/vim/vimfiles/syntax/ragel.vim
}
