# Contributor: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>

_realname=libjpeg-turbo
pkgbase=${_realname}
pkgname="${_realname}"
pkgver=3.0.4
pkgrel=1
pkgdesc="JPEG image codec with accelerated baseline compression and decompression"
arch=('i686' 'x86_64')
url="https://libjpeg-turbo.virtualgl.org/"
msys2_repository_url="https://github.com/libjpeg-turbo/libjpeg-turbo"
msys2_pgp_keys_url="https://libjpeg-turbo.org/Downloads/DigitalSignatures"
msys2_references=(
  "cpe: cpe:/a:d.r.commander:libjpeg-turbo"
  "cpe: cpe:/a:libjpeg-turbo:libjpeg-turbo"
)
license=(custom:'BSD-like')
depends=("gcc-libs")
makedepends=('gcc'
             "cmake"
             "ninja"
             $( [[ "${CARCH}" == "aarch64" ]] || echo "nasm" ))
provides=("libjpeg")
conflicts=("libjpeg")
replaces=("libjpeg")
source=(https://github.com/libjpeg-turbo/libjpeg-turbo/releases/download/${pkgver}/libjpeg-turbo-${pkgver}.tar.gz{,.sig})
sha256sums=('99130559e7d62e8d695f2c0eaeef912c5828d5b84a0537dcb24c9678c9d5b76b'
            'SKIP')
validpgpkeys=('0338C8D8D9FDA62CF9C421BD7EC2DBB6F4DBF434')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  #patch -p1 -i ${srcdir}/
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM_CARCH}" && cd "${srcdir}/build-${MSYSTEM_CARCH}"

  declare -a _extra_config
  if check_option "debug" "n"; then
    _extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    _extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  ${MSYSTEM_PREFIX}/bin/cmake \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=${MSYSTEM_PREFIX} \
    ${_extra_config[@]} \
    -DWITH_JPEG8=ON \
    ../${_realname}-${pkgver}

  ${MSYSTEM_PREFIX}/bin/cmake.exe --build .
}

check() {
  cd "${srcdir}/build-${MSYSTEM_CARCH}"
  ${MSYSTEM_PREFIX}/bin/ctest || true
}

package() {
  cd "${srcdir}/build-${MSYSTEM_CARCH}"

  DESTDIR="${pkgdir}" ${MSYSTEM_PREFIX}/bin/cmake.exe --install .

  cp "${srcdir}"/${_realname}-${pkgver}/{jinclude,transupp}.h ${pkgdir}${MSYSTEM_PREFIX}/include/

  # Licenses
  # See https://www.libjpeg-turbo.org/About/License
  cd "${srcdir}/${_realname}-${pkgver}"
  install -Dm644 README.ijg            "${pkgdir}${MSYSTEM_PREFIX}/share/licenses/${_realname}/README.ijg"
  install -Dm644 README.md  "${pkgdir}${MSYSTEM_PREFIX}/share/licenses/${_realname}/README.md"
  install -Dm644 LICENSE.md  "${pkgdir}${MSYSTEM_PREFIX}/share/licenses/${_realname}/LICENSE.md"
  install -Dm644 simd/nasm/jsimdext.inc "${pkgdir}${MSYSTEM_PREFIX}/share/licenses/${_realname}/simd/jsimdext.inc"
}
