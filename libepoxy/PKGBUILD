# Contributor: Ignacio Casal Quinteiro

_realname=libepoxy
pkgbase=${_realname}
pkgname="${_realname}"
pkgver=1.5.10
pkgrel=5
pkgdesc="A library for handling OpenGL function pointer management for you"
arch=('i686' 'x86_64')
url="https://github.com/anholt/libepoxy"
license=("spdx:MIT")
depends=('gcc-libs'
         'egl-headers'
         'gles-headers')
makedepends=('gcc'
             'pkgconf'
             'python'
             'meson')
options=('strip' 'staticlibs')
source=("${_realname}-${pkgver}.tar.gz::https://github.com/anholt/libepoxy/archive/${pkgver}.tar.gz"
        "001-egl-missing-prototype.patch"
        "https://github.com/anholt/libepoxy/pull/285.patch")
sha256sums=('a7ced37f4102b745ac86d6a70a9da399cc139ff168ba6b8002b4d8d43c900c15'
            '5fb6b63e93cb92052f25c0ac230f3b4d89054a28e02dc37a42eb9f866cd0cbc1'
            'e86994609738190e9fb84ea96184639a1282ee4eee5a5ae0272d9a2d83a802ce')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ../001-egl-missing-prototype.patch
  patch -p1 -i ../285.patch
}

build() {
  [[ -d "${srcdir}/build-${MSYSTEM}" ]] && rm -rf "${srcdir}/build-${MSYSTEM}"
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  meson setup \
    --prefix="${MSYSTEM_PREFIX}" \
    --buildtype plain \
    -Ddefault_library=both \
    -Degl=yes \
    -Dx11=false \
    ../${_realname}-${pkgver}

  meson compile
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  DESTDIR="${pkgdir}" meson install

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MSYSTEM_PREFIX}/share/licenses/${_realname}/COPYING"
}
