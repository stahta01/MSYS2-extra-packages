# ArchLinux Maintainer:  Maxime Gauduin
# ArchLinux Contributor: Tom Gundersen
# ArchLinux Contributor: John Proctor

pkgname=libogg
pkgver=1.3.5
pkgrel=3
pkgdesc='Ogg bitstream and framing library'
arch=('i686' 'x86_64')
url=https://www.xiph.org/ogg/
license=(BSD)
depends=()
makedepends=(
  cmake
  gcc
  git
  ninja
)
#provides=(libogg.so)
source=(git+https://github.com/xiph/ogg.git?signed#tag=v${pkgver})
sha256sums=('SKIP')
validpgpkeys=(B7B00AEE1F960EEA0FED66FB9259A8F2D2D44C84) # Ralph Giles <giles@thaumas.net>

prepare() {
  cd ogg
}

build() {
  cmake -S ogg -B build -G Ninja \
    -DCMAKE_POLICY_VERSION_MINIMUM=3.5 \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON
  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
  install -Dm 644 ogg/COPYING -t "${pkgdir}"/usr/share/licenses/${pkgname}/
  install -Dm 644 ogg/ogg.m4 -t "${pkgdir}"/usr/share/aclocal/
}

# vim: ts=2 sw=2 et:
