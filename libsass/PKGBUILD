# Contributor: Christoph Reiter <reiter.christoph@gmail.com>

_realname=libsass
pkgbase=${_realname}
pkgname="${_realname}"
pkgver=3.6.6
pkgrel=1
pkgdesc="C implementation of Sass CSS preprocessor (library)"
arch=('i686' 'x86_64')
url="https://sass-lang.com/libsass"
msys2_repository_url="https://github.com/sass/libsass"
msys2_references=(
  "cpe: cpe:/a:libsass:libsass"
  "cpe: cpe:/a:sass-lang:libsass"
)
license=("spdx:MIT")
depends=('gcc-libs')
makedepends=('gcc'
             'autotools')
source=("https://github.com/sass/$_realname/archive/${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('11f0bb3709a4f20285507419d7618f3877a425c0131ea8df40fe6196129df15d')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  autoreconf -vfi
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM_CARCH}" && cd "${srcdir}/build-${MSYSTEM_CARCH}"

  ../${_realname}-${pkgver}/configure \
    --prefix=${MSYSTEM_PREFIX} \
    --build=${MSYSTEM_CHOST} \
    --host=${MSYSTEM_CHOST} \
    --target=${MSYSTEM_CHOST} \
    --enable-shared \
    --enable-static

  make
}

package() {
  cd "${srcdir}/build-${MSYSTEM_CARCH}"

  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/COPYING \
    "${pkgdir}"${MSYSTEM_PREFIX}/share/licenses/${_realname}/COPYING
}
